pkgname=brave-bin
pkgver=1.66.118
pkgrel=1
pkgdesc='Brave Web Browser'
arch=(x86_64)
url=https://brave.com
license=(MPL-2.0)
depends=(alsa-lib
         gtk3
         libxcb
         libxcomposite
         libxcursor
         libxdamage
         libxext
         libxfixes
         libxi
         libxrender
         libxss
         libxtst
         libxshmfence
         libxrandr
         libxau
         libxdmcp
         libxinerama
         libxkbcommon
         nspr
         dbus
         expat
         pango
         cairo
         nss
         atk
         at-spi2-core
         at-spi2-atk
         libffi
         gnutls
         zlib
         fribidi
         harfbuzz
         fontconfig
         pixman
         libpng
         libepoxy
         libbsd
         libunistring
         libtasn1
         nettle
         gmp
         libidn2
         bzip2
         ttf-font
         )
optdepends=('cups: Printer support'
            'libgnome-keyring: Enable GNOME keyring support'
            'libnotify: Native notification support')
provides=("${pkgname%-bin}=$pkgver" 'brave-browser')
conflicts=("${pkgname%-bin}")
options=(!strip)
source=(brave-bin.desktop brave-bin.sh)
source_x86_64=(${pkgname}-${pkgver}-x86_64.zip::https://github.com/brave/brave-browser/releases/download/v${pkgver}/brave-browser-${pkgver}-linux-amd64.zip)

noextract=(${pkgname}-${pkgver}-x86_64.zip ${pkgname}-${pkgver}-aarch64.zip)
sha256sums=('b15c0935d8c614a8f6c242a53757d97dd3bb097f13cb7aa46bf50918fe5b05fa'
            '29bff992fd78ee4254786c6e65b546cdadd05afd4ab4b875dcaa3480af0d503a')
sha256sums_x86_64=('4b9fa54df633c556bcea9ef164510a76bf461c769c4097e216abc1eaacba1f9e')

prepare() {
	mkdir -p brave
	bsdtar -xf "$pkgname-$pkgver-$CARCH.zip" -C brave
	chmod +x brave/brave
}

package() {
	install -dm0755 "$pkgdir/opt"
	cp -a brave "$pkgdir/opt/$pkgname"

	# allow firejail users to get the suid sandbox working
	chmod 4755 "$pkgdir/opt/$pkgname/chrome-sandbox"

        install -Dm0755 "$pkgname.sh" "$pkgdir/usr/bin/$pkgname"
	install -Dm0644 -t "$pkgdir/usr/share/applications/" "brave-bin.desktop"
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" brave/LICENSE
	pushd "$pkgdir/usr/"
        for size in 16x16 24x24 32x32 48x48 64x64 128x128 256x256; do
		install -Dm0644 "$pkgdir/opt/$pkgname/product_logo_${size/x*/}.png" \
			"share/icons/hicolor/$size/apps/brave-desktop.png"
	done
        
}
